# Copyright 2019 The Dawn Authors
# Copyright 2021 The WebNN-native Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("../../scripts/webnn_overrides_with_defaults.gni")

import("${webnn_dawn_root}/scripts/dawn_component.gni")
import("${webnn_root}/generator/webnn_generator.gni")

###############################################################################
# WebNN headers
###############################################################################

webnn_json_generator("headers_gen") {
  target = "headers"
  outputs = [
    "include/webnn/webnn_proc_table.h",
    "include/webnn/webnn.h",
  ]
}

source_set("headers") {
  all_dependent_configs = [ ":public" ]
  public_deps = [ ":headers_gen" ]

  sources = get_target_outputs(":headers_gen")
}

###############################################################################
# WebNN C++ headers
###############################################################################

webnn_json_generator("cpp_headers_gen") {
  target = "cpp_headers"
  outputs = [ "include/webnn/webnn_cpp.h" ]
}

source_set("cpp_headers") {
  public_deps = [
    ":cpp_headers_gen",
    ":headers",
  ]

  sources = get_target_outputs(":cpp_headers_gen")
  sources += [ "${webnn_root}/include/webnn/EnumClassBitmasks.h" ]
}

###############################################################################
# WebNN public include directories
###############################################################################
config("public") {
  include_dirs = [
    "${target_gen_dir}/../../include",
    "${webnn_root}/include",
  ]

  if (build_with_chromium) {
    include_dirs += [
      "${webnn_dawn_root}/include",
      "${dawn_gen_root}/include",
    ]
  } else {
    # TODO: Remove after upgrading webnn infranstructure align with dawn.
    include_dirs += [
      "${webnn_dawn_root}/src/include",
      "${dawn_gen_root}/src/include",
    ]
  }
}
